# Maintainer: FÃ©lix Faisant <xcodexif@xif.fr>

pkgname=ioslavesd
pkgver=3.6.4
pkgrel=1
pkgdesc='Xif Network - ioslaves control program daemon for slave side'
license=('custom')
arch=('i686' 'x86_64')
depends=('socketxx' 'libconfig' 'miniupnpc>=1.9' 'topp')
makedepends=('xifutils')
backup=('etc/ioslavesd/ioslavesd.conf' 'etc/ioslavesd/services/ssh.service')
install='ioslavesd.install'

source=('../common/common.hpp' '../common/common.cpp' '../common/log.h' '../common/log_defimpl.h'
        '../common-ioslavesd/api.h'
        '../common-master/master.hpp' '../common-master/master-impl.cpp'
        'main.h' 'slave.cpp' 'upnp.cpp' 'log_slave.cpp' 'status.cpp' 'keys.cpp'
        'Makefile'
        'ioslavesd.service' 'ioslavesd.install' 'ioslavesd.conf' 'ssh.service')
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP'
            'SKIP'
            'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
            'SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP')

build() {
	cat > $srcdir/defs.h <<- EODEFS
		#define IOSLAVESD_ETC_DIR               "/etc/ioslavesd"
		#define IOSLAVESD_API_DL_DIR            "/usr/lib/ioslavesd/api-services"
		#define IOSLAVESD_LOG_FILE              "/var/log/ioslavesd.log"
		#define IOSLAVESD_RUN_FILES             "/var/run"
		#define IOSLAVESD_PERSISTANT_RUN_FILES  "/var/db"
		#define IOSLAVESD_KEYS_DIR              IOSLAVESD_ETC_DIR"/keys"
		#define IOSLAVES_MASTER_DIR             "/var/ioslaves/ioslaves-master"
		#define IOSLAVES_MASTER_KEYS_DIR        IOSLAVES_MASTER_DIR"/keys"
		#define IOSLAVESD_VERSION               "${pkgver}"
		#define XIFNET_SLAVES_DOM               "net.xif.fr"
EODEFS
	make CXXFLAGS="${CXXFLAGS} -include defs.h"
}

package() {
	install -D -m754 $srcdir/ioslavesd 		$pkgdir/usr/bin/ioslavesd
	install -D -m644 $srcdir/ioslavesd.service 	$pkgdir/usr/lib/systemd/system/ioslavesd.service
	install -d -m700 				$pkgdir/etc/ioslavesd/keys
	install -d -m755 				$pkgdir/etc/ioslavesd/services
	install -D -m644 $srcdir/ssh.service 		$pkgdir/etc/ioslavesd/services/ssh.service
	install -D -m644 $srcdir/ioslavesd.conf 	$pkgdir/etc/ioslavesd/ioslavesd.conf
	install -d -m755 				$pkgdir/usr/lib/ioslavesd/api-services
	install -d -m755				$pkgdir/var/ioslaves/ioslaves-master
}
